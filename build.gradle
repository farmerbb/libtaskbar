buildscript {
    apply from: 'Taskbar/dependencies.gradle'

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$AGP_VERSION"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

apply from: 'Taskbar/dependencies.gradle'
apply plugin: 'com.android.library'

repositories {
    google()
}

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode VERSION_CODE
        versionName VERSION_NAME

        buildConfigField "String", "APPLICATION_ID", "\"com.farmerbb.taskbar\""
        buildConfigField "String", "BASE_APPLICATION_ID", "\"com.farmerbb.taskbar\""
        buildConfigField "String", "PAID_APPLICATION_ID", "\"com.farmerbb.taskbar.paid\""
        buildConfigField "String", "SUPPORT_APPLICATION_ID", "\"com.farmerbb.taskbar.support\""
        buildConfigField "String", "ANDROIDX86_APPLICATION_ID", "\"com.farmerbb.taskbar.androidx86\""

        buildConfigField "int", "VERSION_CODE", "$VERSION_CODE"

        buildConfigField "long", "TIMESTAMP", "${System.currentTimeMillis()}L"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java { srcDirs('Taskbar/app/src/main/java', 'Taskbar/app/src/lib/java', 'Taskbar/app/src/nonplaystore/java', "Taskbar/app/src/compat-$COMPILE_SDK_VERSION/java") }
            res { srcDirs('Taskbar/app/src/main/res', 'Taskbar/app/src/lib/res') }
            manifest.srcFile 'Taskbar/app/src/lib/AndroidManifest.xml'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation "androidx.legacy:legacy-support-v4:$SUPPORT_V4_VERSION"
    implementation "androidx.appcompat:appcompat:$APPCOMPAT_VERSION"
    implementation "com.google.android.material:material:$MATERIAL_VERSION"
    implementation "org.lsposed.hiddenapibypass:hiddenapibypass:$HIDDEN_API_BYPASS_VERSION"
}

def getLibVersion() {
    def appVersion = VERSION_NAME
    if(appVersion.split("\\.").length == 2)
        appVersion = "$appVersion.0"

    def libVersion = appVersion.split("\\.")
    libVersion[0] = "${libVersion[0].toInteger() - 4}".toString()

    return libVersion.join('.')
}

tasks.register("outputVersion") {
  doLast {
    println getLibVersion()
  }
}
